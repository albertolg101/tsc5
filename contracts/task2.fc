#include "imports/stdlib.fc";

(slice, int) ~load_std_addr(slice) asm """
LDMSGADDR SWAP REWRITESTDADDR NIP
""";

(int) preload_std_addr(slice) asm """
LDMSGADDR DROP REWRITESTDADDR NIP
""";

(int) get_std_addr(slice) asm """
REWRITESTDADDR NIP
""";

(slice, cell) unwrap_storage() asm """
c4 PUSH CTOS LDMSGADDR LDDICT DROP
""";

int get_total_share(cell dict) inline {
    int total_share = 0;

    (int key, slice val, int flag) = dict.udict_get_min?(256);

    while (flag) {

        total_share += val~load_uint(32);    

        (key, val, flag) = dict.udict_get_next?(256, key);
    }    

    return total_share;
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return ();
    }

    int op = in_msg_body~load_uint(32);

    slice storage = get_data().begin_parse();
    slice admin = storage~load_msg_addr();
    cell dict = storage~load_dict();
    (slice admin, cell dict) = unwrap_storage();

    ;; add user
    if (op == 0x368ddef3) {
        int sender = in_msg_full.begin_parse().skip_bits(4).preload_std_addr();
        throw_if(120, admin.get_std_addr() != sender);

        in_msg_body~skip_bits(64);
        int addr = in_msg_body~load_std_addr();

        dict~udict_set(256, addr, in_msg_body);

        set_data(begin_cell()
            .store_slice(admin)
            .store_dict(dict)
            .end_cell());
    }

    ;; remove user
    elseif (op == 0x278205c8) {
        int sender = in_msg_full.begin_parse().skip_bits(4).preload_std_addr();
        throw_if(120, admin.get_std_addr() != sender);

        in_msg_body~skip_bits(64);
        int addr = in_msg_body~load_std_addr();

        throw_unless(121, dict~udict_delete?(256, addr));

        set_data(begin_cell()
            .store_slice(admin)
            .store_dict(dict)
            .end_cell());
    }

    ;; split
    elseif (op == 0x68530b3) {
        (int key, slice val, int flag) = dict.udict_get_min?(256);
        throw_unless(122, flag);

        int total_share = dict.get_total_share();

        while (flag) {

            send_raw_message(
                begin_cell()
                    .store_uint(0x18, 6)
                    .store_uint(0x400, 11)
                    .store_uint(key, 256)
                    .store_coins(val.preload_uint(32) * msg_value / total_share)
                    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                    .end_cell(), 
                1);

            (key, val, flag) = dict.udict_get_next?(256, key);
        }
    }

    ;; transfer notification
    elseif (op == 0x7362d09c) {
        (int key, slice val, int flag) = dict.udict_get_min?(256);
        throw_unless(122, flag);

        (_, slice sender) = in_msg_full.begin_parse().skip_bits(4).load_msg_addr();
        (in_msg_body, int amount) = in_msg_body.skip_bits(64).load_coins();
        int total_share = dict.get_total_share();

        while (flag) {

            send_raw_message(
                begin_cell()
                    .store_uint(0x18, 6)
                    .store_slice(sender)
                    .store_coins(20000000)
                    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                    .store_ref(
                        begin_cell()
                            .store_uint(0x0f8a7ea5 << 64, 32 + 64)
                            .store_coins(val.preload_uint(32) * amount / total_share)   
                            .store_uint(0x400, 11)
                            .store_uint(key, 256)
                            .store_uint(0x400, 11)
                            .store_uint(key, 256)
                            .store_uint(0, 1)
                            .store_coins(1)
                            .store_uint(0, 1)
                        .end_cell()
                    )
                    .end_cell(), 
                0);

            (key, val, flag) = dict.udict_get_next?(256, key);
        }
    }
}

cell get_users() method_id {
    slice storage = get_data().begin_parse();
    _ = storage~load_msg_addr();
    return storage~load_dict();
}

int get_user_share(slice user_address) method_id {
    int index = user_address~load_std_addr();
    (slice share, _) = get_users().udict_get?(256, index);
    return share.preload_uint(32);
}